plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id "com.jfrog.bintray" version "1.8.4"
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-io:commons-io:2.6'
    compile 'org.eclipse.jetty:jetty-server:9.4.18.v20190429'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.9'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    compile 'org.slf4j:slf4j-api:1.7.26'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.mockito:mockito-core:2.28.2'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.slf4j:slf4j-simple:1.7.26'
    testCompile 'com.mashape.unirest:unirest-java:1.4.9'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId "org.kodekuality"
            artifactId "wirespy-server"
            version System.getenv('TRAVIS_TAG') ?: "SNAPSHOT"
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['myPublication']
    publish = true
    pkg {
        repo = 'maven'
        name = "wirespy-server"
        userOrg = 'kodekuality'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kodekuality/wirespy.git'

        version {
            name = System.getenv('TRAVIS_TAG') ?: "SNAPSHOT"
            released = new Date()
            vcsTag = System.getenv('TRAVIS_TAG') ?: "SNAPSHOT"
            gpg {
                sign = true
            }
        }
    }
}