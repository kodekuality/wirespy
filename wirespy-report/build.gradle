plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id "com.jfrog.bintray" version "1.8.4"
}

group 'org.kodekuality'
version 'SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile project(":wirespy-client")
    compile 'commons-io:commons-io:2.6'
    compile 'org.reflections:reflections:0.9.11'
    compile 'com.github.jknack:handlebars:4.1.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.hamcrest:hamcrest-core:2.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId "org.kodekuality"
            artifactId "wirespy-report"
            version System.getenv('TRAVIS_TAG') ?: "SNAPSHOT"
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['myPublication']
    publish = true
    pkg {
        repo = 'maven'
        name = "wirespy-report"
        userOrg = 'kodekuality'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kodekuality/wirespy.git'

        version {
            name = System.getenv('TRAVIS_TAG') ?: "SNAPSHOT"
            released = new Date()
            vcsTag = System.getenv('TRAVIS_TAG') ?: "SNAPSHOT"
            gpg {
                sign = true
            }
        }
    }
}